import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektCreateBaselineTask

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'me.moallemi.advanced-build-version'
    id 'com.adarshr.test-logger' version '3.2.0'
    id "io.gitlab.arturbosch.detekt" version "$detekt_version"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "dev.yasan.metro"
        minSdk 21
        targetSdk 32

        versionCode 233
        versionName "2.3.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    def dimenStore = 'store'
    flavorDimensions dimenStore
    productFlavors {
        fdroid {
            dimension dimenStore
            applicationIdSuffix(".fdroid")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.2.0"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}
advancedVersioning.renameOutputApk()

dependencies {
    // YASAN Development Kit
    implementation 'com.github.yasan-org:ydk-android:1.1.1'

    // Misc
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.6.1'

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation 'androidx.activity:activity-compose:1.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    implementation "androidx.navigation:navigation-compose:$lifecycle_version"

    // Accompanist
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-material:$accompanist_version"

    // Lifecycle
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Leak Canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    // Subsampling Scale ImageView
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'

    // Coil
    implementation("io.coil-kt:coil-compose:$coil_version")

    // Persian Date
    implementation 'com.github.samanzamani:PersianDate:1.5.4'

    // Detekt
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detekt_version"
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"

    // Local Unit Tests
    implementation "androidx.test:core:1.4.0"
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation "com.google.truth:truth:$truth_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"

    // Instrumented Unit Tests
    androidTestImplementation "junit:junit:$junit_version"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$mockito_version"
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "com.google.truth:truth:$truth_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

}

detekt {
    buildUponDefaultConfig = true
    allRules = true
    autoCorrect = true
    config = files("$projectDir/detekt/config.yml")
    baseline = file("$projectDir/detekt/baseline.xml")
}

task detektProjectBaseline(type: DetektCreateBaselineTask) {
    description = "Overrides current baseline."
    ignoreFailures.set(true)
    parallel.set(true)
    buildUponDefaultConfig.set(true)
    setSource(files(
            "$rootDir/src/main",
            "$rootDir/src/test"
    ))
    config.setFrom(files("$rootDir/detekt/config.yml"))
    baseline.set(file("$rootDir/detekt/baseline.xml"))
    include("**/*.kt")
    include("**/*.kts")
}

tasks.withType(Detekt).configureEach {
    reports {
        html.required.set(true)
        xml.required.set(false)
        txt.required.set(false)
        sarif.required.set(false)
        md.required.set(true)
    }
}

tasks.withType(Detekt).configureEach {
    jvmTarget = "1.8"
}
tasks.withType(DetektCreateBaselineTask).configureEach {
    jvmTarget = "1.8"
}